# Система прав конфигурации

Для ознакомления стоит сделать отбор по подсистеме "ДокуменооборотПраваДоступа" \(лежит в СтандартныеПодсистемыДокументооборота\)

а там есть дескрипторы ;\)

# Настройка прав через дескрипторы для нового объекта

## Настройка ролей

+++

## Настройка менеджера нового объекта

### Шаблон

```
// Возвращает строку, содержащую перечисление полей доступа через запятую
// Это перечисление используется в дальнейшем для передачи в метод 
// ОбщегоНазначения.ПолучитьЗначенияРеквизитов()
Функция ПолучитьПоляДоступа() Экспорт
    Возврат  "Автор, Подразделение, Ссылка";
КонецФункции

// Заполняет переданный дескриптор доступа 
Процедура ЗаполнитьДескрипторДоступа(ОбъектДоступа, ДескрипторДоступа) Экспорт//+++
    ДескрипторДоступа.ДобавитьПользователя("Ответственный", ОбъектДоступа.Автор);    
    ДескрипторДоступа.Организация = ОбъектДоступа.Подразделение;
КонецПроцедуры    

// Возвращает Истина, указывая тем самы что этот объект сам заполняет свои права 
Функция ЕстьМетодЗаполнитьПраваДоступа() Экспорт      
    Возврат Истина;
КонецФункции

// Заполняет параметр ПраваДоступа правами доступа, вычисляя их на 
// основании переданного дескриптора доступа.
// Если указан параметр ПротоколРасчетаПрав, то в него 
// заносится список данных, которые были использованы для расчета прав.
Процедура ЗаполнитьПраваДоступа(ДескрипторДоступа, ПраваДоступа, ПротоколРасчетаПрав) Экспорт
    Перем Подразделение, ПраваПользователя, Запрос;
    //: ДескрипторДоступа = Справочники.ДескрипторыДоступаОбъектов.ПустаяСсылка(); // объявление типа переменной для снегопата
    УстановитьПривилегированныйРежим(Истина);
    Справочники.ДескрипторыДоступаОбъектов.ЗаполнитьПраваДоступаСтандартно(
        ДескрипторДоступа, 
        ПраваДоступа, 
        ПротоколРасчетаПрав);

    //Чтение доступно всем из подразделения
    //Получить подразделение 
    Подразделение = ДескрипторДоступа.Организация;//Справочники.СтруктураПредприятия.ПустаяСсылка();
    ВыборкаПользователей = ПолучитьВыборкуПользователей(Подразделение);

    //Добавить всем право на чтение
    ПраваПользователя = Новый Структура("Чтение, Добавление, Изменение, Удаление, УправлениеПравами", 
                                     Истина, Ложь, Ложь, Ложь, Ложь);
    Пока ВыборкаПользователей.Следующий() Цикл
        ДокументооборотПраваДоступа.ДобавитьЗаписьВСоответствиеПрав(ПраваДоступа, ВыборкаПользователей.Пользователь, ПраваПользователя);
    КонецЦикла;

    //Права редактора теста
    ПраваПользователя = Новый Структура("Чтение, Добавление, Изменение, Удаление, УправлениеПравами", 
                        Истина, Истина, Истина, Истина, Ложь);

    Для Каждого СтрокаПользователей из ДескрипторДоступа.Пользователи Цикл//туда положен только ответственный, будет еще кто-то с другими правами - надо будет фильтровать табличку
        ДокументооборотПраваДоступа.ДобавитьЗаписьВСоответствиеПрав(ПраваДоступа, СтрокаПользователей.Пользователь, ПраваПользователя);    
    КонецЦикла;

    // Добавление руководителей
    ДокументооборотПраваДоступа.РасширитьПраваПоРуководителям(ПраваДоступа);
КонецПроцедуры
```

Вообще, нужно активно копипастить, но есть вот какие экспортные методы \(примерный код смотрите в шаблоне\):

#### Функция ПолучитьПоляДоступа\(\) Экспорт

Что будет, если накосячить в списке полей, я пока не понял. Для табличной части похоже нужно просто указать её название

#### Процедура ЗаполнитьДескрипторДоступа\(ОбъектДоступа, ДескрипторДоступа\) Экспорт

Настраивает дескриптор доступа по объекту доступа и другим связанным радостям

#### Функция ЕстьМетодЗаполнитьПраваДоступа\(\) Экспорт

Просто должна быть, если права настраиваем сами

#### Процедура ЗаполнитьПраваДоступа\(ДескрипторДоступа, ПраваДоступа, ПротоколРасчетаПрав\) Экспорт

Заполняет "Права доступа" на основании переданного дескриптора доступа. Права доступа это соответствие Ключ - пользователь, значение структура с правами вида Структура\("Чтение, Добавление, Изменение, Удаление, УправлениеПравами"\), все булевы.

Не забывайте про волшебное ДокументооборотПраваДоступа.РасширитьПраваПоРуководителям\(ПраваДоступа\); в конце и привелигированный режим в начале, так как часто это вызывает бесправный пользователь.

## Настройка автоматики

Для того, чтобы дескрипторы сами рисовались как надо нужно включитьновый объект в следующие места:

### Подписки

ДокументооборотПраваДоступаПередЗаписью\[ОбъектаДоступа\|Документа\|НабораЗаписей\]

ДокументооборотПраваДоступаПриЗаписиОбъектаДоступа

### Справочники

Файлы.ВладелецФайла \[если надо работать с файлами\] - добавить новый тип в список типов

### Регистр сведений 

ДескрипторыДоступаДляОбъектов - добавить в измерение Объект тип нового объекта

ДескрипторыДоступаДляФайлов - \[если надо\] добавить в измерение владелец файла тип нового объекта





