## Попытка-исключение с кастомной регистрацией

Коротко, дешево и сердито полная диагностика. Если видов ошибок в области видимости несколько - можно добавить еще один параметр
Спецфункция позволяет укоротить вызов, её нужно переписать под конкретное окружение в области видимости.

```bsl
Попытка
Исключение
    ПредставлениеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
    ДобавитьСообщениеДляЖурналаРегистрации(ПредставлениеОшибки, ФайлДляЗагрузки.ПолноеИмя);
 КонецПопытки;

&НаСервереБезКонтекста
Процедура ДобавитьСообщениеДляЖурналаРегистрации(ПредставлениеОшибки, Данные) 
	ЖурналРегистрации.ДобавитьСообщениеДляЖурналаРегистрации(
		"ЗагрузкаОбработок",
		УровеньЖурналаРегистрации.Ошибка,
		,
		Данные,
		ПредставлениеОшибки
	);
КонецПроцедуры 
```

## Вывод сообщений закрывающейся формы в остающуюся 
\[для поиска: вывод сообщений после закрытия формы\]
(источник https://forum.infostart.ru/forum9/topic102697/)

Ходят слухи, что все работает хорошо, если сообщать из "после записи" на клиенте, но на 8.3.24.1342 это работает только если проходить в пошаговом режиме в отладке, а если не останавливаться - мимо.

Проблема: мы не можем в момент записи отличить "записать" от "записать и закрыть", поэтому не можем быть уверены, что все что выводим через "сообщить" будет выведено на экран, а не исчезнет вместе с закрываемой формой

Идея: в последнем серверном событии записи собираем еще не выведенные сообщения, и складываем в реквизит формы.
В последнем клиентском  - очищаем отобранный список через десятую секунды, если отработает - значит все сообщения выведены в рамках этой формы и больше не нужны
В при закрытии - выводим все сообщения с измененным адресом, чтобы показались привязанными не к этой форме.
```js
&НаСервере
Процедура тн_ПослеЗаписиНаСервереПосле(ТекущийОбъект, ПараметрыЗаписи)
	// добавим в форму реквизит для хранения сообщений к выводу
	ИмяСпискаСообщений = ИмяСпискаСообщений();
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ЭтотОбъект, ИмяСпискаСообщений) Тогда 
		ЭтотОбъект[ИмяСпискаСообщений].Очистить();
	Иначе
		ДобавляемыеРеквизиты = Новый Массив;                                  
		ДобавляемыеРеквизиты.Добавить(новый РеквизитФормы(ИмяСпискаСообщений, Новый ОписаниеТипов("СписокЗначений")));
		ЭтотОбъект.ИзменитьРеквизиты(ДобавляемыеРеквизиты);
	КонецЕсли;
	
	// собираем сообщения
	тмпСообщения = ПолучитьСообщенияПользователю(Ложь);
	Для Каждого Сообщение из тмпСообщения Цикл
		ЭтотОбъект.тмпСообщения.Добавить(Сообщение, Сообщение.Текст);
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура тн_ПослеЗаписиПосле(ПараметрыЗаписи)
	// Если сразу не закроется, то выведется и без нас, можно очищать
	ПодключитьОбработчикОжидания("ОчиститьОтложенныеСообщения", 0.1, Истина);
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьОтложенныеСообщения() Экспорт
	ЭтотОбъект[ИмяСпискаСообщений()].Очистить();
КонецПроцедуры

&НаКлиенте
Процедура тн_ПриЗакрытииПосле(ЗавершениеРаботы)
	Если НЕ ЗавершениеРаботы Тогда
		// Перенаправим собранные при записи объекта,
		// и еще не отображенные пользователю, сообщения в форму,
		// которая станет активной после закрытия этой формы
		// (т.к. в противном случае окно задачи будет закрыто и пользователь не увидит этих сообщений).
		ПустойУИД = Новый УникальныйИдентификатор("00000000-0000-0000-0000-000000000000");
		Для Каждого Элемент Из ЭтотОбъект[ИмяСпискаСообщений()] Цикл
			Сообщение = Элемент.Значение;
			Сообщение.ИдентификаторНазначения = ПустойУИД;
			Сообщение.Сообщить();
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ИмяСпискаСообщений()
	Возврат "тмпСообщения";
КонецФункции
```
### Кажется можно проще, если это наши сообщения. 
Просто не вязать к форме, но не факт #надоСделать проверить, не зря же так делают в ЗУП.РасчетЗарплатыРасширенный
```
            //Отказ от ОбщегоНазначения.СообщитьПользователю из-за фонового проведения

            Сообщение = Новый СообщениеПользователю;

            Сообщение.Текст = ТекстСообщения;

            Сообщение.КлючДанных = Документ;

            Сообщение.Поле = "Объект.Начисления" + "[" + Формат(Выборка.НомерСтрокиДляПроверки - 1, "ЧН=0; ЧГ=0") + "]." + ИмяКолонки;

            Сообщение.Сообщить();
```